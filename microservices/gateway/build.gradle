import org.springframework.boot.buildpack.platform.build.PullPolicy

plugins {
    alias(gradleBuild.plugins.kotlin.spring)
    alias(gradleBuild.plugins.spring.boot)
    alias(gradleBuild.plugins.spring.dependency.management)
}

dependencies {
    implementation libs.bundles.kotlin,
            libs.kotlin.jackson

    implementation libs.spring.boot.actuator,
            libs.spring.boot.security,
            libs.spring.retry

    implementation libs.bundles.resourceServer

    implementation platform(boms.spring.cloud),
            libs.bundles.spring.cloud.base,
            libs.spring.cloud.gateway

    implementation libs.spring.security.oauth2.authorization.server

    implementation project(":util")

    testImplementation testLibs.bundles.test
}

jar {
    enabled = false
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped"]
    }
}

bootBuildImage {
    builder = "paketobuildpacks/builder:base"
    runImage = "paketobuildpacks/run:base-cnb"
    pullPolicy = PullPolicy.IF_NOT_PRESENT
    imageName = "${rootProject.name}/${project.name}"
}

tasks.register("buildImages") {
    dependsOn "bootBuildImage"
}
