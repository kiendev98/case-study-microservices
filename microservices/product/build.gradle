import org.springframework.boot.buildpack.platform.build.PullPolicy

plugins {
    alias(gradleBuild.plugins.kotlin.spring)
    alias(gradleBuild.plugins.spring.boot)
    alias(gradleBuild.plugins.spring.dependency.management)
}

dependencies {
    implementation libs.bundles.kotlin,
            libs.kotlin.jackson,
            libs.kotlin.reactor

    implementation libs.bundles.spring.reactiveWebServer,
            libs.spring.boot.data.mongodb.reactive,
            libs.spring.retry

    implementation platform(boms.spring.cloud),
            libs.bundles.spring.cloud.base

    implementation project(":util"), project(":api")

    testImplementation testLibs.bundles.test,
            testFixtures(project(":util"))
}

jar {
    enabled = false
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped"]
    }
}

bootBuildImage {
    builder = "paketobuildpacks/builder:base"
    runImage = "paketobuildpacks/run:base-cnb"
    pullPolicy = PullPolicy.IF_NOT_PRESENT
    imageName = "${rootProject.name}/${project.name}"
}

tasks.register("buildImages") {
    dependsOn "bootBuildImage"
}
